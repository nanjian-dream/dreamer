== 普通遇怪
{
- 普通战斗次数 < 10:
    ~ temp enemyAtt = RANDOM(3, 6)
    ~ temp enemyHp = RANDOM(5, 10)
    ~ temp enemySpeed = RANDOM(3, 7)
    ~ 怪物类型 = "普通"
- else:
    ~ enemyAtt = RANDOM(5, 6)
    ~ enemyHp = RANDOM(10, 15)
    ~ enemySpeed = RANDOM(5, 8)
    ~ 怪物类型 = "普通精英"
    ~ 普通战斗次数 = 0
}
~ 怪物难度 = enemyAtt
~ 怪物生命 = enemyHp
~ 怪物速度 = enemySpeed
-> 战斗准备

== 特殊遇怪
{
- 特殊战斗次数 < 5:
    ~ temp enemyAtt = RANDOM(6, 8)
    ~ temp enemyHp = RANDOM(10, 15)
    ~ temp enemySpeed = RANDOM(5, 8)
    ~ 怪物类型 = "特殊"
- else:
    ~ enemyAtt = RANDOM(9, 10)
    ~ enemyHp = RANDOM(15, 20)
    ~ enemySpeed = RANDOM(7, 10)
    ~ 怪物类型 = "特殊精英"
    ~ 特殊战斗次数 = 0
}
~ 怪物难度 = enemyAtt
~ 怪物生命 = enemyHp
~ 怪物速度 = enemySpeed
-> 战斗准备

== 战斗准备
{
- 战斗准备 <= 1:
Tips:<br>随机生成敌人，难度3-10，血量5-？，速度3-10。<br>投掷1D10，结果等于或超过怪物速度先攻，否则怪物先攻。<br>攻击方式：投掷1D10，结果超过怪物难度，对怪物造成1点伤害。<br>怪物进攻阶段，可以选择投掷1D10闪避，投掷结果超过怪物速度闪避成功，失败则失去2点生命。或是投掷1D10进行反击，超过怪物难度则反击成功，不受伤害且对怪物造成1点伤害，反击失败则受到3点伤害。如不进行选择，则稳定受到1点伤害。<br>敌我双方任意一方血量归0，战斗结束。<br>我方生命值归零，健康值和理智值-1，下一次遭遇战斗时生命值为1。<br>战斗胜利后，随机获得特殊物品，有几率获得黑色晶体。<br>生命只能在探索中遇到特殊地点，或发生特定事件才能恢复。 # CLASS: tips
}
{~ 雾气逐渐减弱|你穿过重重迷雾|你在可见度极地的浓雾中游荡，时而听到野兽的嘶吼}，{~ 一头巨大的怪兽突兀地出现在你的面前，不由分说向你发起攻击|怪兽猛然从阴影中钻出，巨臂一甩，将你甩飞很远|数条包裹着黏糊液体的触手骤然朝你侵袭而来}！
<hr>
{showFight()}
<hr>
+ [投掷1D10（检定攻击顺序）]
~ temp 战斗顺序 = RANDOM(1, 10)
投掷1D10结果：{战斗顺序}
{
- 战斗顺序 >= 怪物速度:
{~ 玩家开始进攻|进入玩家攻击阶段|玩家率先发起攻击}。
-> 我方阶段
- else:
{~ 玩家慢了一步，怪物率先发动攻击|怪物先发制人，趁你还没反应过来，就朝你发起了攻击}。
-> 敌方阶段
}

== 我方阶段
+ [投掷1D10(攻击)]
~ temp 攻击骰 = RANDOM(1, 10)
投掷1D10结果：{攻击骰}
{
- 攻击骰 == 1:
~ 大失败次数 += 1
-> 判断成就 ->
大失败！ # CLASS: red
你试图{~ 攻击怪物|对怪物发起攻击}，不但没有打中，反而直接扑到怪物面前。
怪物轻而易举将你抓起来，{~ 重重砸到地面|狠狠扔到远处}。
生命 -3 # CLASS: red
~ 生命 -= 3
    {
    - 生命 > 0:
    生命：{生命} # CLASS: yellow
    + [怪物回合]
    -> 敌方阶段
    - else:
    ~ 生命 = 0
    生命：{生命} # CLASS: yellow
    你的生命值归零，战斗结束。
    健康 -1，理智 -1 # CLASS: red
    ~ 健康 -= 1
    ~ 理智 -=1
    ~ 战败次数 += 1
    -> 判断成就 ->
    + [战斗结束]
    -> 检查行动次数和探索
    }
- 攻击骰 >= 怪物难度:
你对怪物发起攻击，对怪物造成1点伤害。
~ 怪物生命 -= 1
    {
    - 怪物生命 > 0:
    怪物生命：{怪物生命} # CLASS: yellow
    + [怪物回合]
    -> 敌方阶段
    - else:
    ~ 怪物生命 = 0
    怪物生命：{怪物生命} # CLASS: yellow
    {
    - 怪物类型 == "普通":
    ~ 普通战斗次数 += 1
    ~ 击杀普通怪物数量 += 1
    }
    {
    - 怪物类型 == "普通精英":
    ~ 击杀普通精英数量 += 1
    }
    {
    - 怪物类型 == "特殊":
    ~ 特殊战斗次数 += 1
    ~ 击杀特殊怪物数量 += 1
    }
    {
    - 怪物类型 == "特殊精英":
    ~ 击杀特殊精英数量 += 1
    }
    {~ 你成功打怪了怪物|怪物被你杀死了}！
    -> 判断成就 -> 战斗奖励 -> 检查行动次数和探索
    }
- else:
你对怪物发起攻击，可惜没有对怪物造成有效伤害。
+ [怪物回合]
-> 敌方阶段
}

== 敌方阶段
怪物对你发动攻击。
+ [投掷1D10（闪避）]
~ temp 闪避骰 = RANDOM(1, 10)
投掷1D10（闪避）结果：{闪避骰}
{
- 闪避骰 == 1:
~ 大失败次数 += 1
-> 判断成就 ->
大失败！ # CLASS: red
你尝试闪开怪物的攻击，不料{~ 躲闪失败|左脚拌右脚}，直接扑到怪物面前。
怪物轻而易举将你抓起来，{~ 重重砸到地面|狠狠扔到远处}。
生命 -3 # CLASS: red
~ 生命 -= 3
    {
    - 生命 > 0:
    生命：{生命} # CLASS: yellow
    + [怪物回合]
    -> 敌方阶段
    - else:
    ~ 生命 = 0
    生命：{生命} # CLASS: yellow
    你的生命值归零，战斗结束。
    健康 -1，理智 -1 # CLASS: red
    ~ 健康 -= 1
    ~ 理智 -=1
    ~ 战败次数 += 1
    -> 判断成就 ->
    + [战斗结束]
    -> 检查行动次数和探索
    }
- 闪避骰 >= 怪物速度:
你一个{~ 懒驴打滚|恶狗扑食|狗吃屎|踉跄|就地翻滚|后跳|侧身}，成功躲闪过怪物的攻击。
+ [玩家回合]
-> 我方阶段
- else:
你试图闪避，但晚了一步，怪物将你打飞，对你造成2点伤害！
~ 生命 -= 2
    {
    - 生命 > 0:
    生命：{生命} # CLASS: yellow
    + [玩家回合]
    -> 我方阶段
    - else:
    ~ 生命 = 0
    生命：{生命} # CLASS: yellow
    你的生命值归零，战斗结束。
    健康 -1，理智 -1 # CLASS: red
    ~ 健康 -= 1
    ~ 理智 -=1
    ~ 战败次数 += 1
    -> 判断成就 ->
    + [战斗结束]
    -> 检查行动次数和探索
    }
}

+ {怪物类型 != "特殊精英"}[投掷1D10（反击）]
~ temp 反击骰 = RANDOM(1, 10)
投掷1D10（反击）结果：{反击骰}
{
- 反击骰 == 1:
~ 大失败次数 += 1
-> 判断成就 ->
大失败！ # CLASS: red
你朝怪物扑过去，主动发起反击，不仅打空，还把自己送到怪物手上。
来不及躲闪，你就遭到重创。
生命 -5 # CLASS: red
~ 生命 -= 5
    {
    - 生命 > 0:
    生命：{生命} # CLASS: yellow
    + [怪物回合]
    -> 敌方阶段
    - else:
    ~ 生命 = 0
    生命：{生命} # CLASS: yellow
    你的生命值归零，战斗结束。
    健康 -1，理智 -1 # CLASS: red
    ~ 健康 -= 1
    ~ 理智 -=1
    ~ 战败次数 += 1
    -> 判断成就 ->
    + [战斗结束]
    -> 检查行动次数和探索
    }
- 反击骰 >= 怪物难度:
你抓住怪物攻击的空当，对怪物发起反击，成功对怪物造成1点伤害！
~ 怪物生命 -= 1
    {
    - 怪物生命 > 0:
    怪物生命：{怪物生命} # CLASS: yellow
    + [玩家回合]
    -> 我方阶段
    - else:
    ~ 怪物生命 = 0
    怪物生命：{怪物生命} # CLASS: yellow
    {~ 你成功打怪了怪物|怪物被你杀死了}！
    {
    - 怪物类型 == "普通":
    ~ 普通战斗次数 += 1
    ~ 击杀普通怪物数量 += 1
    }
    {
    - 怪物类型 == "普通精英":
    ~ 击杀普通精英数量 += 1
    }
    {
    - 怪物类型 == "特殊":
    ~ 特殊战斗次数 += 1
    ~ 击杀特殊怪物数量 += 1
    }
    -> 判断成就 -> 战斗奖励 -> 检查行动次数和探索
    }
- else:
你尝试对怪物发起反击，然而{~ 你慢了一步|，你以为的“空当”并不存在|怪物不给你反抗的机会}。
你不仅反击失败，还遭受到怪物的重创。
生命 -3 # CLASS: red
~ 生命 -= 3
    {
    - 生命 > 0:
    生命：{生命} # CLASS: yellow
    + [玩家回合]
    -> 我方阶段
    - else:
    ~ 生命 = 0
    生命：{生命} # CLASS: yellow
    你的生命值归零，战斗结束。
    健康 -1，理智 -1 # CLASS: red
    ~ 健康 -= 1
    ~ 理智 -= 1
    ~ 战败次数 += 1
    -> 判断成就 ->
    + [战斗结束]
    -> 检查行动次数和探索
    }
}
+ [放弃行动]
你放弃抵抗，遭受1点伤害！
~ 生命 -= 1
{
- 生命 > 0:
生命：{生命} # CLASS: yellow
+ [玩家回合]
-> 我方阶段
- else:
~ 生命 = 0
生命：{生命} # CLASS: yellow
你的生命值归零，战斗结束。
健康 -1，理智 -1 # CLASS: red
~ 健康 -= 1
~ 理智 -= 1
~ 战败次数 += 1
-> 判断成就 ->
+ [战斗结束]
-> 检查行动次数和探索
}

== 战斗奖励
{
- 怪物类型 == "普通" || 怪物类型 == "普通精英":
~ temp 普通怪物获得奖励 = RANDOM(1, 100)
~ temp 普通物品 = RANDOM(1, 100)
    {
    - 普通怪物获得奖励 > 60:
    黑色晶体 +1 # CLASS: orange
    ~ 黑色晶体 += 1
    - else:
        {
        - 普通物品 <= 10:
        腐烂触手 +1 # CLASS: orange
        ~ 腐烂触手 += 1
        - 普通物品 <= 20:
        紫色软石 +1 # CLASS: orange
        ~ 紫色软石 += 1
        - 普通物品 <= 30:
        恶臭尖牙 +1 # CLASS: orange
        ~ 恶臭尖牙 += 1
        - 普通物品 <= 40:
        翻涌脓疱 +1 # CLASS: orange
        ~ 翻涌脓疱 += 1
        - 普通物品 <= 50:
        菌丝眼泡 +1 # CLASS: orange
        ~ 菌丝眼泡 += 1
        - 普通物品 <= 60:
        棘皮刺尾 +1 # CLASS: orange
        ~ 棘皮刺尾 += 1
        - 普通物品 <= 70:
        跃动心脏 +1 # CLASS: orange
        ~ 跃动心脏 += 1
        - 普通物品 <= 80:
        蠕动内脏 +1 # CLASS: orange
        ~ 蠕动内脏 += 1
        - 普通物品 <= 90:
        腐骨残指 +1 # CLASS: orange
        ~ 腐骨残指 += 1
        - else:
        黑硬触角 +1 # CLASS: orange
        ~ 黑硬触角 += 1
        }
    }
}
{
- 怪物类型 == "特殊" || 怪物类型 == "特殊精英":
~ temp 获得黑色晶体 = RANDOM(5, 10)
黑色晶体 +{获得黑色晶体} # CLASS: orange
~ 黑色晶体 += 获得黑色晶体
}
{
- 怪物类型 == "特殊精英":
~ temp 获得咒印碎片概率 = RANDOM(1, 100)
    {
    - 获得咒印碎片概率 >= 99:
    咒印碎片 +1 # CLASS: orange
    ~ temp 咒印碎片 = RANDOM(1, 5)
        {咒印碎片:
        - 1:
        ~ 咒印碎片一 = true
        - 2:
        ~ 咒印碎片二 = true
        - 3:
        ~ 咒印碎片三 = true
        - 4:
        ~ 咒印碎片四 = true
        - 5:
        ~ 咒印碎片五 = true
        }
    }
}
->->