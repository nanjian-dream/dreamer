# theme: dark
# author: 南间梦
INCLUDE var&fun.ink
INCLUDE events.ink
INCLUDE special.ink
INCLUDE fight.ink
INCLUDE pasture.ink

+[更新日志]
-> 更新日志
+ [游戏说明]
-> 游戏说明
+ [进入游戏]
-> 进入游戏

== 更新日志
<b>【更新日志】</b>
TODO: 特殊事件（已完成）、特殊地点（完成）、种田(完成)、成就（完成）、结局（完成）、最终BOSS(完成)、物品描述
2025-8-29
完成最终BOSS战。
完成所有结局描述文本。
完成游戏。
物品描述未做（不确定是否会做）。
修改已知bug。
\---
2025-8-28
完成成就模块。
\---
2025-8-27
完成牧场模块。
\---
2025-8-26
完成探索地点内容。
\---
2025-8-22
修改、完善战斗系统，增加探索特殊地点遭遇怪物，增加普通精英和特殊精英怪，增加战斗胜利奖励。
\---
2025-8-21
调整部分事件效果。
完成全部每日事件。
\---
2025-8-20
完成事件效果（随机商人未完成）。
\---
2025-8-19
调整完善普通遇怪战斗节点。
完成第二天早上的固定事件。
\---
2025-8-18
完成战斗模块。
去除探索获得物品的大失败，增加获得资源的概率。
完善资源耗尽后降低健康、理智的判断。
\---
2025-8-15
建立游戏。
完成游戏框架。
完成探索获得资源。
+ [游戏说明]
-> 游戏说明
+ [跳过]
-> 进入游戏

== 游戏说明
# CLEAR
<div style="font-size: 20px; font-weight: 700; text-align: center; color: gold">游戏说明</div><hr>
1. 每天可以进行三次行动选择
2. 只有通过探索才能打开新地图
3. 某些资源需要进行骰子判定，成功才能获得，失败不能获得，大失败(ROLL点为1)有惩罚，大成功(满点)有额外奖励
4. 生存前10天每天会消耗1点食物、1点水和1点电力资源<br>11-30天每天消耗2点，60天后消耗3点
5. 每天执行的操作顺序：消耗资源 \-> 每日事件 \-> 选择行动
6. 食物和水耗尽后，第二天开始，每天降低1点健康值，直到再次获得食物和水停止
7. 食物耗尽3天，或者水耗尽2天后，游戏结束
8. 电力耗尽后，可能发生可怕事件，理智会降低
9. 生命值只在战斗环节生效。健康值超过10点，可以增加1点生命值
10. 健康和理智中任意一项耗尽，或找到出口离开，游戏结束
最后，祝游戏愉快！

+ [进入游戏]
-> 进入游戏

== 进入游戏
# CLEAR
就像做了个漫长、遥无止境的梦。
当你清醒过来，身处于一座破旧的小木屋中。
屋子的摆设十分简单：一张很硬的木板床，一张低矮的方桌，匹配着一张需要屈着腿才能坐稳的椅子。
除此之外，还有一座提供热量的壁炉和一盏垂吊在天花板随时可能掉下来的灯。
桌上放着一张纸，潦草的字迹提醒你，小屋外笼罩着无尽的浓雾，你需要从迷雾中寻找生存必须的资源。
然而，雾中还潜藏着各种不为人知的可怕危险，随时可能丢掉性命。
给你留下讯息的“前辈”告诉你，壁炉只是摆设，你需要从雾中寻找到<span style="color: yellow; font-weight: 700">“黑色晶体”</span>，作为壁炉的“食物”，才能给你提供热和电。
你不知道将会在这里呆多久，陪伴你的只有一枚六面骰和两枚十面骰。
要么在孤独和恐惧中死去，要么找到出路——一切都掌握在你的手中。
也许如此……
+ [开始]
~ 健康 = RANDOM(3, 7)
~ 理智 = RANDOM(3, 7)
~ 生命 = RANDOM(5, 10)
-> 木屋

== 木屋
# CLEAR
{showStatus()}
{
- 生存天数 >= 10 && !只是开始:
达成成就：只是开始 # CLASS: ach
~ 成就数量 += 1
~ 只是开始 = true
}
{
- 生存天数 >= 50 && !雾都孤儿:
达成成就：雾都孤儿 # CLASS: ach
~ 成就数量 += 1
~ 雾都孤儿 = true
}
{
- 生存天数 >= 100 && !此即吾乡:
达成成就：此即吾乡 # CLASS: ach
~ 成就数量 += 1
~ 此即吾乡 = true
}
{
- 牧场模块 && !拓荒者:
达成成就：拓荒者 # CLASS: ach
}
破烂的小木屋，似乎被遗弃了很久，勉强能够遮风挡雨。
窗户用木条杂乱无章地钉起来，只能通过缝隙观察外面的情况。
- (top)
+ (选择休息)[休息]
木板床很硬。
{
- 选择休息 <= 1:
Tips:<br>随机恢复1点健康，或一点理智。 # CLASS: tips
}
~ temp 休息恢复 = RANDOM(1, 6)
{
- 休息恢复 <= 3:
你在床上翻来覆去，勉强睡了一觉，疲惫的身躯轻松了一点。
健康 +1 # CLASS: orange
~ 健康 += 1
- else:
你睡得并不安稳，好在还是有些效果，精神恢复了一些。
理智 +1 # CLASS: orange
~ 理智 += 1
}
-> 一天结束

+ [充电]
{
- 黑色晶体 > 0:
你将一枚黑色晶体塞如壁炉中，壁炉里的“火光”明亮了一些。
黑色晶体 -1 # CLASS: red
电力 +3 # CLASS: orange
~ 黑色晶体 -= 1
~ 电力 += 3
- else:
黑色晶体不足！# CLASS: red
}
-> top

+ [查看仓库]
-> 仓库

+ {成就数量 > 0} [查看成就]
-> 成就

+ (离家){行动次数 > 0}[出门]
    ++ [探索]
    -> 探索
    ++ {可搜寻地点 > 0} [搜寻资源]
    -> 搜寻
    ++ {牧场模块}[牧场]
    -> 牧场
    ++ [回屋]
    -> 木屋

== 仓库
# CLEAR
+ {黑色晶体 > 0}[黑色晶体：{黑色晶体}]
    ++ [确定]
    -> 仓库
    + {腐烂触手 > 0}[腐烂触手：{腐烂触手}]
    ++ [确定]
    -> 仓库
+ {紫色软石 > 0}[紫色软石：{紫色软石}]
    ++ [确定]
    -> 仓库
+ {恶臭尖牙 > 0}[恶臭尖牙：{恶臭尖牙}]
    ++ [确定]
    -> 仓库
+ {翻涌脓疱 > 0}[翻涌脓疱：{翻涌脓疱}]
    ++ [确定]
    -> 仓库
+ {菌丝眼泡 > 0}[菌丝眼泡：{菌丝眼泡}]
    ++ [确定]
    -> 仓库
+ {棘皮刺尾 > 0}[棘皮刺尾：{棘皮刺尾}]
    ++ [确定]
    -> 仓库
+ {跃动心脏 > 0}[跃动心脏：{跃动心脏}]
    ++ [确定]
    -> 仓库
+ {蠕动内脏 > 0}[蠕动内脏：{蠕动内脏}]
    ++ [确定]
    -> 仓库
+ {腐骨残指 > 0}[腐骨残指：{腐骨残指}]
    ++ [确定]
    -> 仓库
+ {黑硬触角 > 0}[黑硬触角：{黑硬触角}]
    ++ [确定]
    -> 仓库
+ {尖叫草种子数量 > 0}[尖叫草种子：{尖叫草种子数量}]
    ++ [确定]
    -> 仓库
+ {金傀灵种子数量 > 0}[金傀灵种子：{金傀灵种子数量}]
    ++ [确定]
    -> 仓库
+ {陆螅种子数量 > 0}[陆螅种子：{陆螅种子数量}]
    ++ [确定]
    -> 仓库
+ {寄缚藓种子数量 > 0}[寄缚藓种子：{寄缚藓种子数量}]
    ++ [确定]
    -> 仓库
+ {迷魂花种子数量 > 0}[迷魂花种子：{迷魂花种子数量}]
    ++ [确定]
    -> 仓库
+ {蛛心藤种子数量 > 0}[蛛心藤种子：{蛛心藤种子数量}]
    ++ [确定]
    -> 仓库
+ {腐心菇种子数量 > 0}[腐心菇种子：{腐心菇种子数量}]
    ++ [确定]
    -> 仓库
+ {黏液布丁种子数量 > 0}[黏液布丁种子：{黏液布丁种子数量}]
    ++ [确定]
    -> 仓库
+ {淤虫鱼幼苗数量 > 0}[淤虫鱼幼苗：{淤虫鱼幼苗数量}]
    ++ [确定]
    -> 仓库
+ {多旗鱼幼苗数量 > 0}[多旗鱼幼苗：{多旗鱼幼苗数量}]
    ++ [确定]
    -> 仓库
+ {线腮鱼幼苗数量 > 0}[线腮鱼幼苗：{线腮鱼幼苗数量}]
    ++ [确定]
    -> 仓库
+ {骨鱼幼苗数量 > 0}[骨鱼幼苗：{骨鱼幼苗数量}]
    ++ [确定]
    -> 仓库
+ {腐眼鱼幼苗数量 > 0}[腐眼鱼幼苗：{腐眼鱼幼苗数量}]
    ++ [确定]
    -> 仓库
+ {尖叫草数量 > 0}[尖叫草：{尖叫草数量}]
    ++ [确定]
    -> 仓库
+ {金傀灵数量 > 0}[金傀灵：{金傀灵数量}]
    ++ [确定]
    -> 仓库
+ {陆螅数量}[陆螅：{陆螅数量}]
    ++ [确定]
    -> 仓库
+ {寄缚藓数量 > 0}[寄缚藓：{寄缚藓数量}]
    ++ [确定]
    -> 仓库
+ {迷魂花数量 > 0}[迷魂花：{迷魂花数量}]
    ++ [确定]
    -> 仓库
+ {蛛心藤数量 > 0}[蛛心藤：{蛛心藤数量}]
    ++ [确定]
    -> 仓库
+ {腐心菇数量 > 0}[腐心菇：{腐心菇数量}]
    ++ [确定]
    -> 仓库
+ {黏液布丁数量 > 0}[黏液布丁：{黏液布丁数量}]
    ++ [确定]
    -> 仓库
+ {淤虫鱼数量 > 0}[淤虫鱼：{淤虫鱼数量}]
    ++ [确定]
    -> 仓库
+ {多旗鱼数量 > 0}[多旗鱼：{多旗鱼数量}]
    ++ [确定]
    -> 仓库
+ {线腮鱼数量 > 0}[线腮鱼：{线腮鱼数量}]
    ++ [确定]
    -> 仓库
+ {骨鱼数量 > 0}[骨鱼：{骨鱼数量}]
    ++ [确定]
    -> 仓库
+ {腐眼鱼数量 > 0}[腐眼鱼：{腐眼鱼数量}]
    ++ [确定]
    -> 仓库
+ {真知之眼 > 0}[真知之眼：{真知之眼}]
    ++ [确定]
    -> 仓库
+ {生命泉}[真知之眼-生命泉]
    ++ [使用]
    ~ 生命泉 = false
    -> 恢复点_生命泉
    ++ [返回]
    -> 仓库
+ {幻湖}[真知之眼-幻湖]
    ++ [使用]
    ~ 幻湖 = false
    -> 恢复点_幻湖
    ++ [返回]
    -> 仓库
+ {梦幻谷}[真知之眼-梦幻谷]
    ++ [使用]
    ~ 梦幻谷 = false
    -> 恢复点_梦幻谷
    ++ [返回]
    -> 仓库
+ [关闭]
-> 木屋

== 探索
# CLEAR
{
- 探索 <= 1:
    Tips: <br>投掷1D100，1-60获得资源，61-80遭遇怪物，81-85恢复点，86-100发现特殊地点。 # CLASS: tips
}
~ 行动次数 -= 1
你在迷雾中提心吊胆前行，无法辨别前进的方向，甚至不能确定是在前进，还是在后退。
唯一能确定的是，某个方向就是你能回去的地方，那座小木屋的所在地。
确认能够回去，你多少安心了一点。
+ (投掷探索地点) [投掷1D100]
~ temp 1D100 = RANDOM(1, 100)
投掷1D100结果：{1D100}
{
- 1D100 >= 1 && 1D100 <= 60:
迷雾中忽然闪动奇异的光点。
你受到光点的吸引而靠近，获得了资源。
{
- 投掷探索地点 <= 1:
    Tips: <br>投掷两次1D6。<br>第一次投掷决定获得资源类型：1-3获得食物，4-6获得水。<br>第二次投掷决定获得数量：6点大成功，获得2单位资源；1-5成功，获得1单位资源。 # CLASS: tips
}
+ [投掷1D6（资源类型）]
~ temp 资源类型 = RANDOM(1, 6)
投掷1D6结果：{资源类型}
{
- 资源类型 <= 3:
    ++ [投掷1D6（获得数量）]
    ~ temp 食物数量 = RANDOM(1, 6) 
    投掷获得数量结果：{食物数量}
    {
    - 食物数量 <= 5:
        成功！ # CLASS: orange
        食物 +1 # CLASS: orange
        ~ 食物 += 1
        -> 检查行动次数和探索
    - 食物数量 == 6:
        大成功！ # CLASS: yellow
        食物 +2 # CLASS: orange
        ~ 大成功次数 += 1
        ~ 食物 += 2
        -> 判断成就 -> 检查行动次数和探索
    }
- else:
    ++ [投掷1D6（获得数量）]
    ~ temp 水数量 = RANDOM(1, 6) 
    投掷获得数量结果：{水数量}
    {
    - 水数量 <= 5:
        成功！ # CLASS: orange
        水 +1 # CLASS: orange
        ~ 水 += 1
        -> 检查行动次数和探索
    - 水数量 == 6:
        大成功！ # CLASS: yellow
        水 +2 # CLASS：orange
        ~ 大成功次数 += 1
        ~ 水 += 2
        -> 判断成就 -> 检查行动次数和探索
    }
}

- 1D100 > 60 && 1D100 <= 80:

-> 普通遇怪

- 1D100 > 80 && 1D100 <= 85:
前方散发着柔和温软的光芒，仿佛希望所在。
你感觉像是在无边无际的烈焰沙漠里行进了三天三夜，突兀地发现一片草木茂盛的绿洲。
你的心里升起希望的火焰，加快速度朝发出光芒的方向跑去。
-> 恢复点

- else:
-> 特殊地点
}

== 检查行动次数和探索
{
- 行动次数 <= 0:
+ [返回木屋]
-> 木屋
- else:
+ [继续探索]
-> 探索
+ [返回木屋]
-> 木屋
}

== 恢复点
{
- 恢复点 <= 1:
Tips: 投掷1D10，2-5恢复1健康，6-9恢复1理智。<br>1大失败，随机失去1健康或1理智。<br>10大成功，恢复1健康和1理智。 # CLASS: tips
}
+ [投掷1D10]
~ temp 恢复类型 = RANDOM(1, 10)
投掷1D10结果：{恢复类型}
{
- 恢复类型 == 1:
    大失败！ # CLASS: red
    ~ 大失败次数 += 1
    -> 判断成就 ->
    你以为是幸运之所，不料却是不祥之地。
    靠近之时，你才发现是一片{~ 雷云缠绕的黑暗之地|瘴气弥漫的谷地|翻滚着腥臭浓烟的地方}。
    刚踏足那片区域，你就感到头晕目眩，一头栽倒在地。
    等你清醒过来，光芒已然消失不见，你重新回到浓浓的迷雾之中。
    健康 -1，理智 -1 # CLASS: red
    -> 检查行动次数和探索
- 恢复类型 <= 4:
    -> 恢复点_生命泉 -> 检查行动次数和探索
- 恢复类型 <= 7:
    -> 恢复点_幻湖 -> 检查行动次数和探索
- 恢复类型 == 9:
    -> 恢复点_梦幻谷 -> 检查行动次数和探索
- else:
    大成功！ # CLASS: orange
    ~ 大成功次数 += 1
    -> 判断成就 ->
    你被光芒包裹起来，身上暖洋洋的。
    不知道发生了什么，光芒消失后，你感觉整个人好了很多。
    健康 +1，理智+1，生命 +1 # CLASS: orange
    ~ 健康 += 1
    ~ 理智 += 1
    ~ 生命 += 1
    -> 检查行动次数和探索
}

== 恢复点_生命泉
靠近那个地方，你{~一个踉跄摔了一跤，摔进一个清澈的泉水里。<br>清凉的泉水洗涤你的疲惫，你身上的伤痕不知不觉减少了。|的眼前骤然出现一道华丽的瀑布，水花翻滚着落入碧绿的泉水里。<br>你站在瀑布下方，利用冰凉清澈的泉水洗了把脸，忍不住用手捧着喝了几口。<br>甘甜的泉水滋润你的身心，你惊讶地发现疲惫减少，甚至身上的伤痕都消失了。}
你还想继续感受泉水的奇妙，恍神之间，泉水竟然不见了，你又回到浓雾之中。
健康 +1 # CLASS: orange
~ 健康 += 1
->->

== 恢复点_幻湖
一个大湖突兀地出现在你的面前。
湖水碧蓝如洗，依稀有鱼——大概是吧——跃出水面。
清风拂过水面，荡漾起粼粼波光，令人心旷神怡。
明明没有太阳，这里却格外光亮。
你坐在湖边，悠闲地度过片刻。
理智 +1 # CLASS: orange
~ 理智 += 1
->->

== 恢复点_梦幻谷
这里是……
一道山谷赫然出现在你的脚下。
谷中开满了色彩艳丽的花朵，绿草如茵。
穿过山谷，一眼就能看到坐落在山顶的白色建筑。
远远看去，那是一座辉煌的宫殿。
宫殿很小很小，意味着离你非常遥远，想要抵达那里根本不可能。
“那是什么地方呢？有没有希望到那里探索一番？”
你这样想着，后知后觉身体在香甜的空气中轻松很多。
健康 +1，理智 +1 # CLASS: orange
~ 健康 += 1
~ 理智 += 1
->->

== 特殊地点
~ temp 检定特殊地点 = RANDOM(1, 100)
~ temp 检定地点 = RANDOM(1, 100)
~ temp 检定出口 = RANDOM(1, 100)
~ temp 部分特殊地点 = RANDOM(1, 9)
{
- 检定特殊地点 == 100:
    {
    - 检定出口 == 100:
    -> 出口
    - else:
    -> 特殊地点
    }
- else:
    {
    - 检定地点 <= 5:
    -> 特殊地点_祭祀山丘
    - 检定地点 <= 20:
    -> 特殊地点_迷雾酒馆
    - else:
        {部分特殊地点:
        - 1:
            -> 特殊地点_黑暗森林
        - 2:
            -> 特殊地点_腐败农田
        - 3:
            -> 特殊地点_黑溪谷
        - 4:
            -> 特殊地点_陆螅地原
        - 5:
            -> 特殊地点_扭曲丛林
        - 6:
            -> 特殊地点_侵蚀地穴
        - 7:
            -> 特殊地点_腐骨溶洞
        - 8:
            -> 特殊地点_蚀月雾海
        - 9:
            -> 特殊地点_空寂古堡
        }
    }
}



== 搜寻
{黑暗森林: + [黑暗森林] -> 黑暗森林}
{腐败农田: + [腐败农田] -> 腐败农田}
{永死河: + [永死河] -> 永死河}
{祭祀山丘: + [祭祀山丘] -> 祭祀山丘}
{空寂古堡: + [空寂古堡] -> 空寂古堡}
{黑溪谷: + [黑溪谷] -> 黑溪谷}
{无尽洞窟: + [无尽洞窟] -> 无尽洞窟}
{陆螅地原: + [陆螅地原] -> 陆螅地原}
{废墟林地: + [废墟林地] -> 废墟林地}
{扭曲丛林: + [扭曲丛林] -> 扭曲丛林}
{无尽地海: + [无尽地海] -> 无尽地海}
{葬魂台: + [葬魂台] -> 葬魂台}
{侵蚀地穴: + [侵蚀地穴] -> 侵蚀地穴}
{腐骨溶洞: + [腐骨溶洞] -> 腐骨溶洞}
{蚀月雾海: + [蚀月雾海] -> 蚀月雾海}
{迷雾酒馆: + [迷雾酒馆] -> 迷雾酒馆}
+ [返回]
-> 木屋

== 判断成就
{
- 结局一 && 结局二 && 结局三 && 结局四 && 结局五 && 结局六 && 结局七 && !无尽轮回:
达成成就：无尽轮回 # CLASS: ach
    {
    - 成就数量 >= 29:
    达成成就：这就是真爱 # CLASS: ach
    ~ 这就是真爱 = true
    ~ 成就数量 += 1
    }
}
{
- E1 && E2 && E3 && E4 && E5 && E6 && E7 && E8 && E9 && E10 && E11 && E12 && E13 && E14 && E15 && E16 && E17 && E18 && E19 && E20 && !完满旅程:
达成成就：完满旅程 # CLASS: ach
~ 完满旅程 = true
~ 成就数量 += 1
}
{
- 击杀普通怪物数量 >= 30 && !猎杀者:
达成成就：猎杀者 # CLASS: ach
~ 猎杀者 = true
~ 成就数量 += 1
}
{
- 击杀普通精英数量 >= 20 && !我是高手:
达成成就：我是高手 # CLASS: ach
~ 我是高手 = true
~ 成就数量 += 1
}
{
- 击杀特殊怪物数量 >= 30 && !生存大师:
达成成就：生存大师 # CLASS: ach
~ 生存大师 = true
~ 成就数量 += 1
}
{
- 击杀特殊精英数量 >= 20 && !我即主宰:
达成成就：我即主宰 # CLASS: ach
~ 我即主宰 = true
~ 成就数量 += 1
}
{
- 黑暗森林 && 腐败农田 && 空寂古堡 && 黑溪谷 && 陆螅地原 && 扭曲丛林 && 侵蚀地穴 && 腐骨溶洞 && 蚀月雾海 && 迷雾酒馆 && !迷雾旅者:
达成成就：迷雾旅者 # CLASS: ach
~ 迷雾旅者 = true
~ 成就数量 += 1
}
{
- 种植次数 >= 100 && !腐坏之地:
达成成就：腐坏之地 # CLASS: ach
~ 腐坏之地 = true
~ 成就数量 += 1
}
{
- 养殖次数 >= 100 && !深渊水族馆:
达成成就：深渊水族馆 # CLASS: ach
~ 深渊水族馆 = true
~ 成就数量 += 1
}
{
- 大失败次数 >= 50 && !神弃者:
达成成就：神弃者 # CLASS: ach
~ 神弃者 = true
~ 成就数量 += 1
}
{
- 大成功次数 >= 50 && !神之眷顾:
达成成就：神之眷顾 # CLASS: ach
~ 神之眷顾 = true
~ 成就数量 += 1
}
{
- 战败次数 >= 10 && !失败者:
达成成就：失败者 # CLASS: ach
~ 失败者 = true
~ 成就数量 += 1
}
{
- 战败次数 >= 50 && !受虐者的狂欢:
达成成就：受虐者的狂欢 # CLASS: ach
~ 受虐者的狂欢 = true
~ 成就数量 += 1
}

->->

== 成就
# CLEAR
{成就数量} / 30<hr>
+ [生存]
{只是开始: 【生存10天】只是开始}
{雾都孤儿: 【生存50天】雾都孤儿}
{此即吾乡: 【生存100天】此即吾乡}
    ++ [退出]
    -> 成就
+ [探索]
{迷雾旅者: 【成功记录全部探索位置】迷雾旅者}
{回归现实: 【找到出口放弃离开】回归现实}
{信徒: 【祭祀10次】信徒}
{眷族: 【祭祀50次】眷族}
{神选者: 【成功召唤神祇】神选者}
    ++ [退出]
    -> 成就
+ [种田]
{拓荒者: 【解锁种田系统】拓荒者}
{蠕农: 【初次开垦】蠕农}
{潜渊者: 【初次挖掘池塘】潜渊者}
{腐坏之地: 【种植100次】腐坏之地}
{深渊水族馆: 【养殖100次】深渊水族馆}
    ++ [退出]
    -> 成就
+ [战斗]
{猎杀者: 【迷雾击杀怪物30只】猎杀者}
{我是高手: 【迷雾击杀精英怪20只】我是高手}
{生存大师: 【探索击杀怪物30只】生存大师}
{我即主宰: 【探索击杀精英20只】我即主宰}
{失败者: 【战斗失败10次】失败者}
{受虐者的狂欢: 【战斗失败50次】受虐者的狂欢}
    ++ [退出]
    -> 成就
+ [结局]
{魂葬迷雾: 【结局一】魂葬迷雾}
{堕入疯狂: 【结局二】堕入疯狂}
{湮灭: 【结局三】湮灭}
{幻或真: 【结局四】幻或真}
{低语永随: 【结局五】低语永随}
{低语永随: 【结局六】低语永随}
{终将归来: 【结局七】终将归来}
    ++ [退出]
    -> 成就
+ [其他]
{神弃者: 【大失败50次】神弃者}
{神之眷顾: 【大成功50次】神之眷顾}
{完满旅程: 【触发全部每日事件】完满旅程}
{这就是真爱: 【全成就达成】这就是真爱}
    ++ [退出]
    -> 成就
+ [关闭]
-> 木屋

== 一天结束
// + [新的一天]
~ 生存天数 += 1
{
- 生存天数 <= 10:
~ 食物 -= 1
~ 水 -= 1
~ 电力 -= 1
- 生存天数 <= 30:
~ 食物 -= 2
~ 水 -= 2
~ 电力 -= 2
- 生存天数 > 60:
~ 食物 -= 3
~ 水 -= 3
~ 电力 -= 3
}
~ 行动次数 = 3
{
- 尖叫草种子数量 > 0 || 金傀灵种子数量 > 0 || 陆螅种子数量 > 0 || 寄缚藓种子数量 > 0 || 迷魂花种子数量 > 0 || 蛛心藤种子数量 > 0 || 腐心菇种子数量 > 0 || 黏液布丁种子数量 > 0: 
~ 牧场模块 = true
~ 农田模块 = true
}
{
- 淤虫鱼幼苗数量 > 0 || 多旗鱼幼苗数量 > 0 || 线腮鱼幼苗数量 > 0 || 骨鱼幼苗数量 > 0 || 腐眼鱼幼苗数量 > 0:
~ 牧场模块 = true
~ 池塘模块 = true
}
-> 资源耗尽

== 资源耗尽
{
- 食物 <= 0:
~ 食物 = 0
~ 食物耗尽天数 -= 1
~ 健康 -= 1
- else:
~ 食物耗尽天数 = 4
}
{
- 水 <= 0:
~ 水 = 0
~ 水耗尽天数 -= 1
~ 健康 -= 1
- else:
~ 水耗尽天数 = 3
}
{
- 电力 <= 0:
~ 电力 = 0
~ 电力耗尽天数 -= 1
~ 理智 -= 1
}
{
// - 健康 <= 0 || 理智 <= 0 || 食物耗尽天数 <= 0 || 水耗尽天数 <= 0:
- 健康 <= 0 || 理智 <= 0:
-> 游戏结束
- else:
+ [每日事件]
-> 每日事件
}

== 每日事件
# CLEAR
{
- 生存天数 == 2:
你昏沉沉睡着，似乎正在做噩梦，被一阵沉重漫长的敲门声惊醒。
混沌的脑子猜测“客人”是什么人，你全然没有警惕心，一步深一步浅地走到门前，拉开本来就没有门栓的木门。
门口站着一个人。
灰色兜帽罩袍笼罩着他的身体，你既看不到他的脸，也不能确定他的身高。
看上去他跟你差不多高，甚至比你矮一点，但总有种他故意弯曲着腿或身体的感觉。
你正在思考对方的来意，兜帽人率先说话了。
“我知道你在这里。不要问怎么离开，只有你自己知道。”
对方说了一些模棱两可的话，干涩、粗糙、低沉的声音不像是眼前的人发出来的，而像是来自于很远的地方。
仅凭他的声音，你连他是男是女都搞不清。
甚至于连他是不是人都不能确定。
“也许某天，你将加入我们。也许……”
兜帽人似乎不关心你说不说话，以一种极其平缓、规律地一个字一个字吐出来。
<b>“记住，黑夜属于祂们，不要试图让自己身处于黑暗之中。”</b>
说完之后，兜帽人戴着和衣服一样颜色布手套的手拿出一个布袋子递给你。
“这是见面礼。希望下次来的时候，还能够见到你。”
兜帽人转身离去，消失于迷雾之中。
他离开的突然，正如他的出现一样。
你打开布袋，里面是些生存需要的资源。
食物 +2，水 +2，黑色晶体 +1 # CLASS: orange
~ 食物 += 2
~ 水 += 2
~ 黑色晶体 += 1
+ [第 {生存天数} 天]
-> 木屋
}
{
- 生存天数 == 3:
你正准备出门，“咚咚咚”的敲门声响起来。
敲打木门的声音沉闷，迟缓，似乎门外的人没有那么心急。
打开门后，一个身穿灰黑色罩袍的家伙用略显轻快的语气跟你打招呼。
“早上好，我从附近路过，顺道过来跟你打个招呼。下次再来的话，我可能需要从你手里‘收取’一些东西。不要用那种眼光看我，我是有职业道德的商人，收获付钱是常识。相信我，我能给的价格比你想的高。当然，你也可以从我手里换一些用得上的东西。”
一口气“嘚吧嘚吧”说完，他也不等你回应，甩下一句“希望下次来的的时候，这个屋里还有人”就离开了。
你还没明白到底是怎么回事，只是能确定他和昨天遇到那个人不一样。
不仅因为他们身形不同，说话的语气不同，而且穿着颜色也不完全相同。
最重要的是，商人没有戴手套，左手手背上印着一个非常特别的印记。
无论如何，希望下次遇见他，可以收获一些帮助生存的东西。
+ [第 {生存天数} 天]
-> 木屋
}
{
- 生存天数 > 3:
    {
    - 迷雾酒馆:
        {~ -> 事件1 | -> 事件2 | -> 事件3 | -> 事件4 | -> 事件5 | -> 事件6 | -> 事件7 | -> 事件8 | -> 事件9 | -> 事件10 | -> 事件11 | -> 事件12 | -> 事件13 | -> 事件14 | -> 事件15 | -> 事件16 | -> 事件17 | -> 事件18 | -> 事件19 | -> 事件20}
    - else:
        {~ -> 事件1 | -> 事件2 | -> 事件3 | -> 事件4 | -> 事件5 | -> 事件6 | -> 事件7 | -> 事件8 | -> 事件9 | -> 事件10 | -> 事件11 | -> 事件12 | -> 事件13 | -> 事件14 | -> 事件15 | -> 事件16 | -> 事件17 | -> 事件18 | -> 事件19}
    }
}

== 出口
光点刺穿迷雾，继而扩张、放大。
当你走到光点前，光点已然“成长”为一道足以让巨人穿过的高大门扉。
门扉闪耀着无法形容的光彩，如同诱惑飞蛾般吸引着你。
站在门前，你看到门后的世界。
熟悉的高楼大厦，熟悉的宽阔街道，熟悉的车流穿梭，熟悉的人潮涌动。
还有那个熟悉的地方，你的家。
你找到了出口，如果不离开，下次找到出口不知道是什么时候。
也许只有这一次机会。
但是，迷雾中还有更多谜题，你也可以留下来解开。
+ [继续冒险]
不知为何，你发现靠近门时，一直缠绕你的声音消失了。
尽管是好事，异变仍让你惴惴不安。
你对迷雾中是否有谜题不感兴趣，可也不想愚蠢地扎进未知的时空。
这地方已经足够可怕，你没有余力迎接新的冒险。
{
- !回归现实:
达成成就：回归现实 # CLASS: ach
~ 成就数量 += 1
~ 回归现实 = true
} 
-> 检查行动次数和探索
+ [离开]
你受够了这个鬼地方，无论迷雾中隐藏着什么惊世骇俗的秘密，你都毫无兴趣。
你只想离开，永远地逃离比地狱更加可怖的地方。
于是，你毫不犹豫，一头扎进光门里。
宛如断翅的蝴蝶般漂浮，坠落。
你看到门后迷雾中的世界。
仅是一瞥，便看到那座无比庞大的身影。
一只眼睛——和月球一般巨大的眼睛——没有瞳孔如虚空的眼睛转向了你。
{
- !幻或真:
达成成就：幻或真 # CLASS: ach
~ 成就数量 += 1
~ 幻或真 = true
}
    ++ [结局四]
    ~ 结局四 = true
    -> 游戏结束

== 游戏结束
# CLEAR
{
-生命 <= 0 && 理智 > 0: 
~ 结局一 = true
    {
    - !魂葬迷雾:
    ~ 魂葬迷雾 = true
    ~ 成就数量 += 1
    }
-> J1
}
{
-理智 <= 0 && 生命 > 0: 
~ 结局二 = true
    {
    - 堕入疯狂:
    ~ 堕入疯狂 = true
    ~ 成就数量 += 1
    }
-> J2
}
{
-理智 <= 0 && 生命 <= 0: 
~ 结局三 = true
    {
    - !湮灭:
    ~ 湮灭 = true
    ~ 成就数量 += 1
    }
-> J3
}
{
- 结局四:
-> J4
}
{
- 永恒幻梦:
~ 结局五 = true
-> J5
}
{
- 结局六:
~ 低语永随 = true
~ 成就数量 += 1
-> J6
}
{
- 结局七:
~ 终将归来 = true
~ 成就数量 += 1
-> J7
}

= J1
你死了。
尽管你从一开始就猜到，迟早会落入这一步结局。
对你而言，脱离煎熬或许是一种解脱。
亲人在你的房间发现你干枯的尸体。
他们不知你经历了什么，只知道弥留时的你，嘴角擒着释怀的笑容。
结局一：魂葬迷雾 # CLASS: ending 
-> 判断成就 ->
+ [从头再来]
-> 重来
+ [游戏结束]
游戏结束 # CLASS: end
-> END

= J2
阳光洒进纯白的房间。
你难得安静地坐在窗边，盯着灼目的烈日。
他们说你疯了。
你确定你没疯，只是向他们展现你的崇拜而已。
你每天都在诉说迷雾中的可怖经历，用诡异的语言传颂福音。
当卧室、家里被疯狂的文字和符号布满，你的家人也离你而去。
即便如此，他们也要饱受你的骚扰，不得不将你送进精神病院。
病房和家，对你来说没有差别，只是传播福音的对象变了而已。
你疯狂地倾诉着终日盘旋在脑海中语言，在这个纯白的监狱中高唱圣歌。
结局二：堕入疯狂 # CLASS: ending 
-> 判断成就 ->
+ [从头再来]
-> 重来
+ [游戏结束]
游戏结束 # CLASS: end
-> END

= J3
坦白地说，消失并不可怕。
你不能解释现在处于什么状态。
也许你是迷雾的一部分，也许你成为一只怪物……
管他妈的，随便什么都好。
毕竟，其实你压根就不会有解释的意愿。
从一开始，你就是这里的一部分。
如今，你只是以原本的姿态回到这里罢了。
结局三：湮灭 # CLASS: ending 
-> 判断成就 ->
+ [从头再来]
-> 重来
+ [游戏结束]
游戏结束 # CLASS: end
-> END

= J4

结局四：幻或真 # CLASS: ending 
-> 判断成就 ->
+ [从头再来]
-> 重来
+ [游戏结束]
游戏结束 # CLASS: end
-> END

= J5
你坐在沙发上，桌上的茶杯飘起红茶的香气。
过去也许发生了什么，你完全没有记忆。
你每天重复着枯燥的生活，偶尔会静下心，凝视远处的天空。
那片天终日灰蒙蒙的。
云层稀薄的话，偶尔能一窥红月的虚影。
你扔下手中的笔，将无意识画下印记的纸扔进垃圾桶，端起茶杯，轻啜入喉。
结局五：永恒幻梦 # CLASS: ending 
-> 判断成就 ->
+ [从头再来]
-> 重来
+ [游戏结束]
游戏结束 # CLASS: end
-> END

= J6
工作，回家，跟家人说说话，睡觉……
你每天重复着枯燥的生活。
偶尔你会静下心来想想，是否发生过什么特别的故事。
答案当然是没有。
即便询问，家人也只会当你又犯傻了，劝你多休息。
只有你自己清楚，每个夜晚，梦境降临，你都会看到瑰丽异常的山谷。
山谷尽头的高山上，耸立着一座白色的巨型宫殿。
低沉的呢喃来自于宫殿之中，永远伴随着你。
结局六：低语永随 # CLASS: ending 
-> 判断成就 ->
+ [从头再来]
-> 重来
+ [游戏结束]
游戏结束 # CLASS: end
-> END

= J7
或是是个故事，或是是个事故。
管他呢。
你打败了神……咔咔，当然不是。
你很清楚，那不过是未知的遥远时空投射的一道幽影而已。
当你激动地挥舞手臂庆祝，才颓丧地发现这个现实。
不过已经不重要了。
还是你熟悉的环境，还是你熟悉的生活。
平凡，单调，枯燥。
日复一日。
要说有什么不同，就是桌上不知什么时候多了一个小木屋的精致装饰。
而你手背上，召唤神祇的神秘印记熠熠生辉，时时灼烧你的灵魂。
结局七：终将归来 # CLASS: ending 
-> 判断成就 ->
+ [从头再来]
-> 重来
+ [游戏结束]
游戏结束 # CLASS: end
-> END

== 重来
// 重置数据
~ 生存天数 = 1
~ 行动次数 = 3
~ 探索次数 = 0
~ 可搜寻地点 = 0

~ 牧场模块 = false
~ 农田模块 = false
~ 池塘模块 = false

~ 地块一 = false
~ 地一名称 = ""
~ 地一时间 = 0

~ 地块二 = false
~ 地二名称 = ""
~ 地二时间 = 0

~ 地块三 = false
~ 地三名称 = ""
~ 地三时间 = 0

~ 地块四 = false
~ 地四名称 = ""
~ 地四时间 = 0

~ 地块五 = false
~ 地五名称 = ""
~ 地五时间 = 0

~ 池塘一 = false
~ 池一名称 = ""
~ 池一时间 = 0

~ 池塘二 = false
~ 池二名称 = ""
~ 池二时间 = 0

~ 池塘三 = false
~ 池三名称 = ""
~ 池三时间 = 0

~ 健康 = 5
~ 理智 = 5
~ 生命 = 10

~ 食物 = 5
~ 水 = 5
~ 电力 = 5

~ 金钱 = 0

~ 食物耗尽天数 = 4
~ 水耗尽天数 = 3
~ 电力耗尽天数 = 3

// 特殊地点
~ 黑暗森林 = false
~ 腐败农田 = false
~ 空寂古堡 = false
~ 黑溪谷 = false
~ 陆螅地原 = false
~ 扭曲丛林 = false
~ 侵蚀地穴 = false
~ 腐骨溶洞 = false
~ 蚀月雾海 = false

~ 祭祀山丘 = false
~ 迷雾酒馆 = false

// 恢复点
~ 幻湖 = false
~ 生命泉 = false
~ 梦幻谷 = false

// 普通怪物
~ 怪物生命 = 1
~ 怪物难度 = 1
~ 怪物速度 = 1
// 普通怪战斗次数
~ 普通战斗次数 = 0
~ 击杀普通怪物数量 = 0
~ 击杀普通精英数量 = 0
// 特殊怪战斗次数
~ 特殊战斗次数 = 0
~ 击杀特殊怪物数量 = 0
~ 击杀特殊精英数量 = 0
// 怪物类型：普通，普通精英，特殊，特殊精英
~ 怪物类型 = ""
~ 战败次数 = 0
// 普通物品
~ 黑色晶体 = 0
~ 腐烂触手 = 0
~ 紫色软石 = 0
~ 恶臭尖牙 = 0
~ 翻涌脓疱 = 0
~ 菌丝眼泡 = 0
~ 棘皮刺尾 = 0
~ 跃动心脏 = 0
~ 蠕动内脏 = 0
~ 腐骨残指 = 0
~ 黑硬触角 = 0
// 特殊位置物品
~ 尖叫草种子数量 = 0
~ 金傀灵种子数量 = 0
~ 陆螅种子数量 = 0
~ 寄缚藓种子数量 = 0
~ 迷魂花种子数量 = 0
~ 蛛心藤种子数量 = 0
~ 腐心菇种子数量 = 0
~ 黏液布丁种子数量 = 0

~ 尖叫草数量 = 0
~ 金傀灵数量 = 0
~ 陆螅数量 = 0
~ 寄缚藓数量 = 0
~ 迷魂花数量 = 0
~ 蛛心藤数量 = 0
~ 腐心菇数量 = 0
~ 黏液布丁数量 = 0

~ 淤虫鱼幼苗数量 = 0
~ 多旗鱼幼苗数量 = 0
~ 线腮鱼幼苗数量 = 0
~ 骨鱼幼苗数量 = 0
~ 腐眼鱼幼苗数量 = 0

~ 淤虫鱼数量 = 0
~ 多旗鱼数量 = 0
~ 线腮鱼数量 = 0
~ 骨鱼数量 = 0
~ 腐眼鱼数量 = 0

~ 祭祀材料植 = 0
~ 祭祀材料动 = 0
~ 祭品列表 = ()

~ 种植次数 = 0
~ 养殖次数 = 0

~ 真知之眼 = 0
// 神祇技能
~ 幽影之触 = false
~ 无光雾影 = false
// 事件触发
~ E1 = false
~ E2 = false
~ E3 = false
~ E4 = false
~ E5 = false
~ E6 = false
~ E7 = false
~ E8 = false
~ E9 = false
~ E10 = false
~ E11 = false
~ E12 = false
~ E13 = false
~ E14 = false
~ E15 = false
~ E16 = false
~ E17 = false
~ E18 = false
~ E19 = false
~ E20 = false
// 咒印碎片
~ 咒印碎片一 = false
~ 咒印碎片二 = false
~ 咒印碎片三 = false
~ 咒印碎片四 = false
~ 咒印碎片五 = false
// ROLL点次数
~ 大失败次数 = 0
~ 大成功次数 = 0
// 酒馆回收确认
~ 回收确认 = false

// 周目 +1
~ 游戏周目 += 1
-> 进入游戏

